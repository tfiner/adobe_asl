/*
    Copyright 2005-2007 Adobe Systems Incorporated
    Distributed under the MIT License (see accompanying file LICENSE_1_0_0.txt
    or a copy at http://stlab.adobe.com/licenses.html)
*/

sheet find_and_replace
{
interface:
    document_open                   : true;
    multi_file_search               : true;

    recent_search_strings;
    use_grep                        : false;
    patterns                        : @add;

    unlink start_at_top             : false <==  multi_file_search ? true : start_at_top;
    unlink wrap_around              : false <==  multi_file_search ? false : wrap_around;
    unlink search_backwards         : false <==  multi_file_search ? false : search_backwards;
    case_sensitive                  : false;

    unlink search_selection_only    : false <==  multi_file_search || !document_open ? false : search_selection_only;
    unlink extend_selection         : false <==  multi_file_search ? true : extend_selection;
    match_entire_words              : false;

    saved_search_sources;

    search_file_type            : @file_type_text;

    unlink multi_file_search_folder : "One Folder: /blah/blah/blah" <== multi_file_search ? multi_file_search_folder : "(nothing to search)";

    /* Options -> Search Nested Folders */
    search_nested_folders           : false;
    unlink skip_paren_folders       : false <== search_nested_folders ? skip_paren_folders : false;
    unlink search_invisible_folders : false <== search_nested_folders ? search_invisible_folders : false;

    /* Options -> Use File Filter */
    use_file_filter             : false;
    unlink file_filter              : @file_filter_none_specified <== use_file_filter ? file_filter : @file_filter_none_specified;

invariant:
    editable_file_filter <== use_file_filter == true;
    pick_multi_file_search <== multi_file_search == true;
}
