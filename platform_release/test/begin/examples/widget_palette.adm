/*
    Copyright 2005-2007 Adobe Systems Incorporated
    Distributed under the MIT License (see accompanying file LICENSE_1_0_0.txt
    or a copy at http://stlab.adobe.com/licenses.html)
*/

sheet widget_palette
{
interface:
    widget_type:      @button;

    name:             "";
    horizontal:       @align_default;
    vertical:         @align_default;

    unlink placement: @place_row <== is_container ? placement : @na;
    unlink spacing:   10 <== is_container ? spacing : 0;
    unlink child_horizontal: @align_default <== is_container ? child_horizontal : @na;
    unlink child_vertical:   @align_default <== is_container ? child_vertical : @na;

    // edit_text and/or edit_number
    characters:       10;
    min_value:        1;
    max_value:        100;

logic:
    relate {
        min_value <== min_value > max_value ? max_value : min_value;
        max_value <== max_value < min_value ? min_value : max_value;
    }

output:
    is_container <== widget_type == @group ||
                     widget_type == @tab_group ||
                     widget_type == @row ||
                     widget_type == @column ||
                     widget_type == @overlay ||
                     widget_type == @optional_panel ||
                     widget_type == @panel;

    result <== { dummy_value: 42 };
}
